apply plugin: 'java'

import org.eclipse.jgit.storage.file.FileRepositoryBuilder
import org.eclipse.jgit.lib.Repository
import jsettlers.textures.generation.TextureGenerationTask


configurations {
    generated
}

def generated_directory = 'src/generated/'

sourceSets {
    generated {
        output.dir(generated_directory + 'java', builtBy: 'findGitRevision')
    }
}


task compileTextures(type: TextureGenerationTask) {
    resourceDirectory = file('raw_images_for_textures')
    generatedSourcesDirectory = file(generated_directory + 'java')
    generatedResourcesDirectory = file(generated_directory + 'resources/')
}


def commitFile = new File(new File(new File(sourceSets.generated.java.srcDirs[0], 'jsettlers'), 'common'), 'CommitInfo.java');
task findGitRevision {
    FileRepositoryBuilder builder = new FileRepositoryBuilder();
    Repository repository = builder.readEnvironment().findGitDir(file('.')).build();
    def headRef = repository.getRef("HEAD")
    def localRev = headRef.objectId.name
    repository.close()

    ext {
        rev = localRev
    }
    onlyIf {
        !commitFile.isFile() || !commitFile.text.contains(rev)
    }
    doLast {
        commitFile.parentFile.mkdirs()
        commitFile.text = sprintf("""package jsettlers.common;
public final class CommitInfo {
	/**
	 * The current commit this was build with, as extracted from git.
	 */
	public static final String COMMIT_HASH_SHORT = "%s";
	public static final String COMMIT_HASH = "%s";
	public static final String BUILD_TIME = "%s";
	private CommitInfo() {
	}
}""", rev.substring(0, 8), rev, (new Date()).format('yyyy-MM-dd-HH-mm-ss'))
    }
}

sourceSets {
    main {
        compileClasspath += sourceSets.generated.output
        runtimeClasspath += sourceSets.generated.output
    }
}

jar {
    from sourceSets.generated.output
    dependsOn generatedClasses
}

compileJava {
    dependsOn compileGeneratedJava
}

compileGeneratedJava {
    dependsOn findGitRevision
    dependsOn compileTextures
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

clean {
    delete commitFile
}

dependencies {
    testImplementation project(':jsettlers.testutils')
}
