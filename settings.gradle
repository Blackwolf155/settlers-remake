
// We cannot share much more with gradle.
gradle.ext.projectsWithExtraTest = []

include ':go.graphics'
include ':go.graphics.swing'
include ':jsettlers.buildingcreator'
include ':jsettlers.common'
include ':jsettlers.graphics'
include ':jsettlers.graphics:layoutbuilder'
include ':jsettlers.graphics.swing'
include ':jsettlers.logic'
include ':jsettlers.main.swing'
include ':jsettlers.mapcreator'
include ':jsettlers.network'
include ':jsettlers.tests'
include ':jsettlers.testutils'
include ':jsettlers.tools'

// Android projects
// Android is automatically disabled if SDK is not set.
def androidFile = new File(rootProject.dir, 'local.properties');
if (System.env.ANDROID_HOME || androidFile.isFile() && androidFile.text.matches('(?s)^(.*\\n)?\\s*sdk.dir=.*'))  {
	include ':go.graphics.android'
	include ':jsettlers.graphics.androidui'
	include ':jsettlers.main.android'
} else {
	println 'WARNING: Android is disabled.'
	println 'To enable Android build, download the Android SDK. Then create a file local.properties with the following content:'
	println '    sdk.dir=<path to sdk>'
}

rootProject.name = 'settlers-remake'
// This will loop through the object descriptors
rootProject.children.each { p ->
	if (new File(p.dir, 'test.gradle').isFile()) {
		gradle.ext.projectsWithExtraTest += [ p.path ];
		def tp = createProjectDescriptor(p, 'test', new File(p.dir, 'testRoot'))
		tp.buildFileName = '../test.gradle'
	}
}
